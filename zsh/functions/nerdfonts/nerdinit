
## vim:ft=zsh ts=2 sw=2 sts=2 et fenc=utf-8

emulate -L zsh
setopt extendedglob
typeset name script
typeset -a match
typeset -gaU nf

# Autoload all i_*.sh functions in fpath
for script in $^fpath/i_*.sh(N); do
  if [[ $script == */i_(#b)(*).sh ]]; then
    name="$match[1]"
    if [[ $name == "all" ]]; then
      continue
    fi

    if [[ -r "$script" ]]; then
      autoload -Uz i_${name}.sh
      nf=("i_${name}.sh" $nf)
    else
      print "Couldn't read file $script containing script $name."
    fi
  else
    print "Eh?  Mismatch between glob patterns in nerdinit."
  fi
done

nerd() {
  eval ${(j:; :)nf}
  unset nf
  # This is a one-off function
  unfunction nerd
}

# Clean up after ourselves
unset name script match
